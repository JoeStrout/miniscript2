==== MiniScript Integration Test Suite
==== This file defines a number of small tests of basic MiniScript compiler
==== and execution tests.  Lines that begin with `====` are comments, but
==== also define test blocks.  A test block is one or more lines that do not
==== start with `====`.  A test block is further divided into input and
==== output, separated by a line starting with `----`.
====
==== The integration test runner should try each test with a fresh MiniScript
==== context, and compare the actual print output to the expected output,
==== flagging a test failure if they do not match.
====
================================================================================
==== SECTION 0: BASIC STATEMENT/COMMENT PARSING
================================================================================
print 42
--------------------------------
42
================================
// Here is a comment all on its own line.
print 123  // and here is a comment after a statement
// Followed by another full-line comment.
--------------------------------
123
================================
// several statements separated by semicolons:
print 1; print 2; print 3
print 4 // however; a semicolon in a comment; does nothing
print 5
--------------------------------
1
2
3
4
5
================================
==== Line continuation after comma, open (, [, or {, or binary operator
================================
print [
  1, 2]
--------------------------------
[1, 2]
================================
print [1,
  2, 3]
--------------------------------
[1, 2, 3]
================================
print (
  6 * 7)
--------------------------------
42
================================
print 40 +
  5 - 3
--------------------------------
42
================================================================================
==== SECTION 1: NUMERIC ARITHMETIC
================================================================================
==== Basic integer operations
================================
print 2+2
--------------------------------
4
================================
print 1+2+3+4
--------------------------------
10
================================
print 100-25-25
--------------------------------
50
================================
print 6*7
--------------------------------
42
================================
print 2 + 3 * 4
--------------------------------
14
================================
print (2 + 3) * 4
--------------------------------
20
================================
print 2*3/4*28
--------------------------------
42
================================
==== Division always returns float in MiniScript
================================
print 7/2
--------------------------------
3.5
================================
print 10/4
--------------------------------
2.5
================================
==== Float arithmetic
================================
print 3.5 + 2.5
--------------------------------
6
================================
print 10.5 - 3.5
--------------------------------
7
================================
print 2.5 * 4
--------------------------------
10
================================
print 7.5 / 2.5
--------------------------------
3
================================
==== Mixed int/float
================================
print 5 + 2.5
print 10.0 - 3
--------------------------------
7.5
7
================================
==== Power operator
================================
print 2^3
--------------------------------
8
================================
print 5+2^3
--------------------------------
13
================================
print 2^10
--------------------------------
1024
================================
print -2^3 // ^ has higher precedence than -
--------------------------------
-8
================================
==== Note: MS 2.0 uses right-associativity for ^, different from MS 1.x
==== MS 1.x: 2^3^2 = (2^3)^2 = 64
==== MS 2.0: 2^3^2 = 2^(3^2) = 512
================================
print 2^3^2
--------------------------------
512
================================
==== Unary minus (see UNARY_MINUS_QUIRK.md)
================================
print -5
--------------------------------
-5
================================
print -3+45
--------------------------------
42
================================
print 45+-3
--------------------------------
42
================================
print 0 - -5
--------------------------------
5
================================
print (-5)
--------------------------------
-5
================================
print -(-5)
--------------------------------
5
================================
==== Modulo
================================
print 10 % 7
--------------------------------
3
================================
print 17 % 5
--------------------------------
2
================================
// mod with negative left operand produces negative result
print -10 % 7
--------------------------------
-3
================================
// negating right operand has no effect on sign
print 10 % -7
--------------------------------
3
================================
==== Number formatting
==== (Matching MiniScript 1.x: 1-6 digits after decimal, exponential for very large/small)
================================
// Repeating decimals should show 6 digits after decimal point
print 10/3
--------------------------------
3.333333
================================
print 1/3
--------------------------------
0.333333
================================
// Trailing zeros should be trimmed (but keep at least one digit after decimal)
print 2.50
--------------------------------
2.5
================================
print 3.10
--------------------------------
3.1
================================
// Integer-valued doubles print as integers
print 1.0
--------------------------------
1
================================
print 6.0/2.0
--------------------------------
3
================================
// Negative zero should print as "0"
print 0-0.0
--------------------------------
0
================================
// Very small numbers use exponential format
print 1/10000000
--------------------------------
1.000000E-07
================================
// Numbers with exactly 6 decimal places
print 123.456789
--------------------------------
123.456789
================================
================================================================================
==== SECTION 2: STRING OPERATIONS
================================================================================
==== String literals
================================
print "hello"
--------------------------------
hello
================================
print "Hello World"
--------------------------------
Hello World
================================
print ""
--------------------------------

================================
==== String concatenation
================================
print "a" + "b"
--------------------------------
ab
================================
print "Hello" + " " + "World"
--------------------------------
Hello World
================================
==== String + number coercion
================================
print "x" + 5
--------------------------------
x5
================================
print "value: " + 42
--------------------------------
value: 42
================================
print 42 + " is the answer"
--------------------------------
42 is the answer
================================
print "" + 42
--------------------------------
42
================================
==== String replication
================================
print "ab" * 3
--------------------------------
ababab
================================
print "x" * 5
--------------------------------
xxxxx
================================
print "test" * 1
--------------------------------
test
================================
print "test" * 0
--------------------------------

================================
==== Fractional string replication
================================
print "abcd" * 0.5
--------------------------------
ab
================================
print "abcd" * 1.5
--------------------------------
abcdab
================================
print "abcdef" * 0.5
--------------------------------
abc
================================
==== String division (same as * inverse)
================================
print "abcd" / 2
--------------------------------
ab
================================
print "abcdef" / 2
--------------------------------
abc
================================
================================================================================
==== SECTION 3: LIST OPERATIONS
================================================================================
==== List literals
================================
print []
--------------------------------
[]
================================
print [1, 2, 3]
--------------------------------
[1, 2, 3]
================================
print [42]
--------------------------------
[42]
================================
==== Mixed type lists
====================================
print [1, "two", 3.5]
--------------------------------
[1, "two", 3.5]
================================
==== Nested lists
================================
print [[1, 2], [3, 4]]
--------------------------------
[[1, 2], [3, 4]]
================================
==== List indexing
================================
print [10, 20, 30][0]
--------------------------------
10
================================
print [10, 20, 30][1]
--------------------------------
20
================================
print [10, 20, 30][2]
--------------------------------
30
================================
==== Negative indexing
====================================
print [10, 20, 30][-1]
print [10, 20, 30][-2]
--------------------------------
30
20
================================
==== Nested list indexing
================================
print [[1, 2], [3, 4]][0]
--------------------------------
[1, 2]
================================
print [[1, 2], [3, 4]][1][0]
--------------------------------
3
================================
================================================================================
==== SECTION 4: MAP OPERATIONS
================================================================================
==== Map literals
================================
print {}
--------------------------------
{}
================================
print {"a": 1}
--------------------------------
{"a": 1}
================================
print {"x": 10, "y": 20}
--------------------------------
{"x": 10, "y": 20}
================================
==== Map indexing
================================
print {"a": 1}["a"]
--------------------------------
1
================================
print {"x": 10, "y": 20}["y"]
--------------------------------
20
================================
==== Numeric keys
================================
print {1: "one", 2: "two"}[1]
--------------------------------
one
================================
print {1: "one", 2: "two"}[2]
--------------------------------
two
================================
==== Mixed key types
================================
print {"name": "test", 1: "one"}["name"]
--------------------------------
test
================================
================================================================================
==== SECTION 5: COMPARISON OPERATIONS
================================================================================
==== Numeric comparisons
================================
print 5 < 10
--------------------------------
1
================================
print 10 < 5
--------------------------------
0
================================
print 5 > 3
--------------------------------
1
================================
print 3 > 5
--------------------------------
0
================================
print 5 <= 5
--------------------------------
1
================================
print 5 <= 6
--------------------------------
1
================================
print 5 >= 5
--------------------------------
1
================================
print 5 >= 4
--------------------------------
1
================================
print 5 == 5
--------------------------------
1
================================
print 5 == 6
--------------------------------
0
================================
print 5 != 6
--------------------------------
1
================================
print 5 != 5
--------------------------------
0
================================
==== Float comparisons
================================
print 3.14 < 3.15
--------------------------------
1
================================
print 2.5 == 2.5
--------------------------------
1
================================
==== Int/float equality
================================
print 5 == 5.0
--------------------------------
1
================================
==== String comparisons (lexicographic)
================================
print "apple" < "banana"
--------------------------------
1
================================
print "banana" < "apple"
--------------------------------
0
================================
print "apple" == "apple"
--------------------------------
1
================================
print "apple" != "banana"
--------------------------------
1
================================
print "a" < "b"
--------------------------------
1
================================
print "abc" < "abd"
--------------------------------
1
================================
================================================================================
==== SECTION 6: LOGICAL OPERATIONS
================================================================================
==== AND operator
====================================
print 1 and 1
--------------------------------
1
====================================
print 1 and 0
--------------------------------
0
====================================
print 0 and 1
--------------------------------
0
====================================
print 0.5 and 1
--------------------------------
0.5
====================================
print 0.5 and 0.5
--------------------------------
0.25
====================================
print 0.5 and -0.5
--------------------------------
0.25
====================================
print 1.5 and 0.5
--------------------------------
0.75
====================================
print -0.5 and -0.5
--------------------------------
0.25
================================================================================
==== OR operator
====================================
print 1 or 0
--------------------------------
1
====================================
print 0 or 1
--------------------------------
1
====================================
print 0 or 0
--------------------------------
0
====================================
print 0.5 or 1
--------------------------------
1
====================================
print 0.5 or 0.5
--------------------------------
0.75
====================================
print -0.1 or -0.5
--------------------------------
0.65
====================================
print -0.5 or 0.5
--------------------------------
0.25
================================================================================
==== NOT operator
====================================
print not 0
--------------------------------
1
====================================
print not 1
--------------------------------
0
====================================
print not 0.25
--------------------------------
0.75
====================================
print not -0.1
--------------------------------
0.9
====================================
print not 42
--------------------------------
0
================================================================================
==== SECTION 7: MULTI-LINE TESTS
================================================================================
==== Multiple print statements
================================
print "Hello"
print "World"
--------------------------------
Hello
World
================================
print 1
print 2
print 3
--------------------------------
1
2
3
================================
================================================================================
==== SECTION 8: VARIABLE TESTS
================================================================================
==== Basic variable assignment and read
================================
x = 42
print x
--------------------------------
42
================================
x = "hello"
print x
--------------------------------
hello
================================
==== Multiple variables
================================
x = 10
y = 20
print x + y
--------------------------------
30
================================
a = 5
b = 3
c = a * b
print c
--------------------------------
15
================================
==== Variable reassignment
================================
x = 1
print x
x = 2
print x
x = 3
print x
--------------------------------
1
2
3
================================
x = 10
x = x + 5
print x
--------------------------------
15
================================
x = 2
x = x * x
print x
--------------------------------
4
================================
==== Variables in expressions
================================
x = 6
y = 7
print x * y
--------------------------------
42
================================
a = 100
b = 58
print a - b
--------------------------------
42
================================
x = 10
y = 4
print x / y + x % y
--------------------------------
4.5
================================
==== Variables with different types
================================
n = 42
s = "answer"
print s + ": " + n
--------------------------------
answer: 42
================================
x = 3.14159
print x * 2
--------------------------------
6.28318
================================
==== Variables in comparisons
================================
x = 5
y = 10
print x < y
--------------------------------
1
================================
a = 42
b = 42
print a == b
--------------------------------
1
================================
==== Variables in logical operations
================================
x = 1
y = 0
print x and y
--------------------------------
0
================================
x = 0
y = 1
print x or y
--------------------------------
1
================================
==== Variables with lists
================================
x = [1, 2, 3]
print x
--------------------------------
[1, 2, 3]
================================
x = [10, 20, 30]
print x[1]
--------------------------------
20
================================
==== Variables with maps
================================
m = {"x": 10}
print m
--------------------------------
{"x": 10}
================================
m = {"a": 1, "b": 2}
print m["a"]
print m["b"]
--------------------------------
1
2
================================
================================================================================
==== SECTION 9: WHILE LOOPS
================================================================================
==== Basic while loop
================================
x = 1
while x < 4
	print x
	x = x + 1
end while
--------------------------------
1
2
3
================================
==== While loop with accumulator
================================
sum = 0
i = 1
while i <= 5
	sum = sum + i
	i = i + 1
end while
print sum
--------------------------------
15
================================
==== While loop - zero iterations (condition false from start)
================================
x = 10
while x < 5
	print "should not print"
end while
print "done"
--------------------------------
done
================================
==== While loop - single iteration
================================
x = 0
while x < 1
	print "once"
	x = x + 1
end while
--------------------------------
once
================================
==== Nested while loops
================================
i = 1
while i <= 2
	j = 1
	while j <= 3
		print i * 10 + j
		j = j + 1
	end while
	i = i + 1
end while
--------------------------------
11
12
13
21
22
23
================================
================================================================================
==== SECTION 10: IF STATEMENTS
================================================================================
==== Single-line if (no else)
================================
x = 5
if x > 3 then print "big"
--------------------------------
big
================================
x = 1
if x > 3 then print "big"
print "done"
--------------------------------
done
================================
==== Single-line if with else
================================
x = 5
if x > 10 then print "very big" else print "not so big"
--------------------------------
not so big
================================
x = 20
if x > 10 then print "very big" else print "not so big"
--------------------------------
very big
================================
x = 15
if x > 10 then if x < 20 then print "mid" else print "not mid"
--------------------------------
mid
================================
==== Block if (no else)
================================
x = 5
if x == 5 then
	print "x is five"
end if
--------------------------------
x is five
================================
x = 3
if x == 5 then
	print "x is five"
end if
print "done"
--------------------------------
done
================================
==== Block if with else
================================
x = -3
if x < 0 then
	print "negative"
else
	print "non-negative"
end if
--------------------------------
negative
================================
x = 7
if x < 0 then
	print "negative"
else
	print "non-negative"
end if
--------------------------------
non-negative
================================
==== Block if/else if/else chains
================================
x = -5
if x < 0 then
	print "negative"
else if x == 0 then
	print "zero"
else
	print "positive"
end if
--------------------------------
negative
================================
x = 0
if x < 0 then
	print "negative"
else if x == 0 then
	print "zero"
else
	print "positive"
end if
--------------------------------
zero
================================
x = 10
if x < 0 then
	print "negative"
else if x == 0 then
	print "zero"
else
	print "positive"
end if
--------------------------------
positive
================================
==== Multiple else if branches
================================
score = 85
if score >= 90 then
	print "A"
else if score >= 80 then
	print "B"
else if score >= 70 then
	print "C"
else if score >= 60 then
	print "D"
else
	print "F"
end if
--------------------------------
B
================================
score = 55
if score >= 90 then
	print "A"
else if score >= 80 then
	print "B"
else if score >= 70 then
	print "C"
else if score >= 60 then
	print "D"
else
	print "F"
end if
--------------------------------
F
================================
==== Nested if statements
================================
a = 3
b = 7
if a > 0 then
	if b > 5 then
		print "a>0 and b>5"
	else
		print "a>0 and b<=5"
	end if
end if
--------------------------------
a>0 and b>5
================================
a = 3
b = 2
if a > 0 then
	if b > 5 then
		print "a>0 and b>5"
	else
		print "a>0 and b<=5"
	end if
end if
--------------------------------
a>0 and b<=5
================================
==== If inside while loop
================================
i = 0
while i < 5
	if i == 2 then print "two"
	i = i + 1
end while
--------------------------------
two
================================
i = 1
while i <= 3
	if i == 1 then
		print "one"
	else if i == 2 then
		print "two"
	else
		print "other"
	end if
	i = i + 1
end while
--------------------------------
one
two
other
================================
==== While inside if statement
================================
x = 1
if x > 0 then
	i = 0
	while i < 3
		print i
		i = i + 1
	end while
end if
--------------------------------
0
1
2
================================
==== Truthiness/falsiness tests
================================
x = 0
if x then print "truthy" else print "falsy"
--------------------------------
falsy
================================
x = 1
if x then print "truthy" else print "falsy"
--------------------------------
truthy
================================
x = -1
if x then print "truthy" else print "falsy"
--------------------------------
truthy
================================
x = 0.5
if x then print "truthy" else print "falsy"
--------------------------------
truthy
================================
==== Logical operators in conditions
================================
x = 5
y = 10
if x > 0 and y > 0 then print "both positive"
--------------------------------
both positive
================================
x = -1
y = 5
if x > 0 or y > 0 then print "at least one positive"
--------------------------------
at least one positive
================================
x = 0
if not x then print "x is falsy"
--------------------------------
x is falsy
================================
s = "abc"
if s then print "truthy" else print "falsy"
--------------------------------
truthy
================================
s = ""
if s then print "truthy" else print "falsy"
--------------------------------
falsy
================================================================================
==== SECTION 11: BREAK STATEMENT
================================================================================
==== Basic break
================================
i = 0
while i < 10
	print i
	if i == 3 then break
	i = i + 1
end while
print "done"
--------------------------------
0
1
2
3
done
================================
==== Break at start of loop
================================
i = 0
while i < 5
	if i == 0 then break
	print "should not print"
	i = i + 1
end while
print "exited"
--------------------------------
exited
================================
==== Break in nested loops (only exits innermost)
================================
i = 0
while i < 3
	j = 0
	while j < 5
		if j == 2 then break
		print i * 10 + j
		j = j + 1
	end while
	i = i + 1
end while
--------------------------------
0
1
10
11
20
21
================================
==== Break with block if
================================
i = 0
while i < 10
	if i > 2 then
		print "breaking"
		break
	end if
	print i
	i = i + 1
end while
--------------------------------
0
1
2
breaking
================================================================================
==== SECTION 12: FOR LOOPS
================================================================================
==== Basic for loop over list
================================
for i in [1, 2, 3]
	print i
end for
--------------------------------
1
2
3
================================
==== For loop over string
================================
for c in "abc"
	print c
end for
--------------------------------
a
b
c
================================
==== For loop with break
================================
for i in [1, 2, 3, 4, 5]
	if i == 3 then break
	print i
end for
print "done"
--------------------------------
1
2
done
================================
==== Nested for loops
================================
for i in [1, 2]
	for j in [10, 20]
		print i * 100 + j
	end for
end for
--------------------------------
110
120
210
220
================================
==== Empty list
================================
for i in []
	print "should not print"
end for
print "done"
--------------------------------
done
================================
==== For loop variable persists after loop
================================
for x in [5, 6, 7]
	y = x + 1
end for
print x + y  // 7 + 8 == 15
--------------------------------
15
================================================================================
==== SECTION 13: CONTINUE STATEMENT
================================================================================
==== Continue in while loop
================================
i = 0
while i < 5
	i = i + 1
	if i == 3 then continue
	print i
end while
--------------------------------
1
2
4
5
================================
==== Continue in for loop
================================
for x in [1, 2, 3, 4, 5]
	if x == 3 then continue
	print x
end for
--------------------------------
1
2
4
5
================================
==== Continue skips rest of body
================================
i = 0
while i < 3
	i = i + 1
	print "before"
	continue
	print "after"
end while
--------------------------------
before
before
before
================================
==== Continue in nested loops (only affects innermost)
================================
for i in [1, 2]
	for j in [10, 20, 30]
		if j == 20 then continue
		print i * 100 + j
	end for
end for
--------------------------------
110
130
210
230
================================================================================
==== SECTION 14: ERROR CASES
================================================================================
==== Runtime error: key not found in map
================================
m = {"a": 1}
print m["b"]
--------------------------------
Runtime Error: Key Not Found: 'b' not found in map
================================
==== Runtime error: index into non-collection
================================
x = 42
print x[0]
--------------------------------
Runtime Error: Can't index into 42
================================
==== Compiler error: break outside loop
================================
break
--------------------------------
Compiler Error: 'break' without open loop block
================================
==== Compiler error: continue outside loop
================================
continue
--------------------------------
Compiler Error: 'continue' without open loop block
================================================================================
==== SECTION 15: FUNCTIONS
================================================================================
==== Basic function definition and call
================================
f = function(x)
	return x * 2
end function
print f(21)
--------------------------------
42
================================
==== Function with multiple params
================================
add = function(x, y)
	return x + y
end function
print add(3, 4)
--------------------------------
7
================================
==== Function with no params (with and without parentheses)
================================
greet = function()
	return "hello"
end function
print greet
--------------------------------
hello
================================
greet2 = function
	return "howdy"
end function
print greet2()
--------------------------------
howdy
================================
==== Return from inside if
================================
f = function(x)
	if x > 0 then return x
	return 0
end function
print f(5)
print f(-3)
--------------------------------
5
0
================================
==== Function with loop
================================
sumTo = function(n)
	result = 0
	i = 1
	while i <= n
		result = result + i
		i = i + 1
	end while
	return result
end function
print sumTo(5)
--------------------------------
15
================================
==== Multiple functions
================================
double = function(x)
	return x * 2
end function
triple = function(x)
	return x * 3
end function
print double(7)
print triple(7)
--------------------------------
14
21
================================
==== Function with multiple arguments (no paren)
================================
printSum = function(x, y)
	print x + y
end function
printSum 10, 32
--------------------------------
42
================================
==== Functions as part of a list or map literal
================================
funcs = [function(x); return x*2; end function,
  function(x)
    return x*3
  end function]
print funcs[0](10)
print funcs[1](10)
--------------------------------
20
30
================================
==== Function with default parameter values
================================
f = function(x, y=10)
	return x + y
end function
print f(5)
print f(5, 20)
--------------------------------
15
25
================================
==== Default string parameter
================================
f = function(s="hi")
	return s
end function
print f
print f("bye")
--------------------------------
hi
bye
================================
==== Default true/false/null
================================
f = function(a=true, b=false, c=null)
	print a
	print b
	print c
end function
f
--------------------------------
1
0
null
================================
==== Negative number default
================================
f = function(x=-5)
	return x
end function
print f
--------------------------------
-5
================================
==== Computed (but still constant)  default
================================
f = function(x=6*7)
	return x
end function
print f
--------------------------------
42
================================
==== Default list (frozen)
================================
f = function(x=[1, 2, 3])
	return x
end function
print f
print isFrozen(f)
--------------------------------
[1, 2, 3]
1
================================
==== Default map (frozen)
================================
f = function(m={"a": 1})
	return m
end function
print f
print isFrozen(f)
--------------------------------
{"a": 1}
1
================================
==== Omitted args get null
================================
f = function(a, b=10)
	print a
	print b
end function
f
--------------------------------
null
10
================================
==== Multiple defaults, some omitted
================================
f = function(a=1, b=2, c=3)
	return a + b + c
end function
print f
print f(10)
print f(10, 20)
print f(10, 20, 30)
--------------------------------
6
15
33
60
================================================================================
==== SECTION 16: FROZEN VALUES
================================================================================
==== isFrozen on unfrozen list and map
================================
x = [1, 2, 3]
print isFrozen(x)
m = {"a": 1}
print isFrozen(m)
--------------------------------
0
0
================================
==== freeze a list, verify isFrozen
================================
x = [1, 2, 3]
freeze x
print isFrozen(x)
--------------------------------
1
================================
==== freeze a map, verify isFrozen
================================
m = {"a": 1}
freeze m
print isFrozen(m)
--------------------------------
1
================================
==== frozen list: remove raises error
================================
x = [1, 2, 3]
freeze x
remove x, 0
--------------------------------
Runtime Error: Attempt to modify a frozen list
================================
==== frozen map: remove raises error
================================
m = {"a": 1, "b": 2}
freeze m
remove m, "a"
--------------------------------
Runtime Error: Attempt to modify a frozen map
================================
==== freeze is recursive
================================
inner = [10, 20]
outer = [inner, 30]
freeze outer
print isFrozen(inner)
--------------------------------
1
================================
==== frozenCopy returns frozen copy without affecting original
================================
x = [1, 2, 3]
y = frozenCopy(x)
print isFrozen(x)
print isFrozen(y)
--------------------------------
0
1
================================
==== frozenCopy of already-frozen returns same value
================================
x = [1, 2, 3]
freeze x
y = frozenCopy(x)
print isFrozen(y)
--------------------------------
1
================================
==== isFrozen on non-collection returns false
================================
print isFrozen(42)
print isFrozen("hello")
print isFrozen(null)
--------------------------------
0
0
0
================================
================================================================================
==== SECTION 17: INDEXED ASSIGNMENT
================================================================================
==== List indexed assignment
================================
lst = [1, 2, 3]
lst[1] = 42
print lst[1]
--------------------------------
42
================================
==== Map indexed assignment (new key)
================================
m = {}
m["foo"] = "bar"
print m["foo"]
--------------------------------
bar
================================
==== Map indexed assignment (overwrite existing key)
================================
m = {"a": 1, "b": 2}
m["a"] = 99
print m["a"]
print m["b"]
--------------------------------
99
2
================================
==== Chained indexed assignment
================================
lst = [[0, 0], [0, 0]]
lst[1][0] = 42
(lst[0])[1] = 123
print lst
--------------------------------
[[0, 123], [42, 0]]
================================
==== Indexed assignment in loop
================================
lst = [0, 0, 0]
for i in [0, 1, 2]
	lst[i] = i * 10
end for
print lst
--------------------------------
[0, 10, 20]
================================
==== Indexed assignment with variable key
================================
m = {}
key = "hello"
m[key] = "world"
print m["hello"]
--------------------------------
world
================================
================================================================================
==== SECTION 18: DOT SYNTAX
================================================================================
==== Basic member access (rvalue)
================================
m = {"x": 10}
print m.x
--------------------------------
10
================================
==== Member assignment (lvalue)
================================
m = {}
m.name = "Bob"
print m.name
--------------------------------
Bob
================================
==== Chained dot access
================================
m = {"a": {"b": 5}}
print m.a.b
--------------------------------
5
================================
==== Dot access and bracket access are equivalent
================================
m = {"key": 42}
print m.key
print m["key"]
--------------------------------
42
42
================================
==== Dot assignment then bracket read and vice versa
================================
m = {}
m.x = 1
print m["x"]
m["y"] = 2
print m.y
--------------------------------
1
2
================================
==== Dot access on nested maps with assignment
================================
m = {"inner": {}}
m.inner.val = 99
print m.inner.val
--------------------------------
99
================================
==== END OF TESTS
================================================================================

